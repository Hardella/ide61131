ext.mpsMajor = "2018.2"
ext.mpsMinor = "5"
ext.mbeddrBuildNumber = "1.1.+"
ext.mpsExtensionsVersion = "$mpsMajor.+"

buildscript {
    repositories {
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
        mavenCentral()
    }
    dependencies {
        classpath 'de.itemis.mps:mps-gradle-plugin:1.0.+'
    }
}

repositories {
    maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    mavenCentral()
    ivy {
        url "https://download.jetbrains.com/mps/$mpsMajor/"
        layout 'pattern', {
            artifact "[module]-[revision].[ext]"
        }
        metadataSources { // skip downloading ivy.xml
            artifact()
        }
    }
}

configurations {
    mps
    mpsPlugins
}

dependencies {
    mps "com.jetbrains:MPS:$mpsMajor.${mpsMinor}@zip"
    mpsPlugins "de.itemis.mps:extensions:$mpsExtensionsVersion"
    mpsPlugins "com.mbeddr:platform:$mbeddrBuildNumber"
}

task downloadDependencies(type: Copy) {
    dependsOn configurations.mps
    dependsOn configurations.mpsPlugins
    from(zipTree(configurations.mps.singleFile)) {
        eachFile {
            // Replace "MPS 2018.2/abc/..." with "mps/abc/..." for predictable folder locations
            it.path = "mps/" + it.path.substring(it.relativePath.segments[0].length())
        }
    } into "lib"
    from {
        configurations.mpsPlugins.collect { zipTree(it) }
    } into "lib"
}

task clean() {
}

def importMpsBuild(fileName, taskPrefix) {
  def antBuilder = antBuilderFactory.create()
  antBuilder.importBuild(fileName) { antTargetName ->
    taskPrefix + antTargetName
  }
  tasks.clean.dependsOn tasks[taskPrefix+'clean']
  antBuilder
}

def languages = importMpsBuild('build.xml', 'languages-')
importMpsBuild('buildDistribution.xml', 'distribution-')

def languages_generate = tasks.'languages-generate'
tasks.'languages-compileJava'.dependsOn.forEach { task ->
  task.getDependencies(tasks.'languages-compileJava').forEach { t ->
    t.dependsOn languages_generate
  }
}

tasks.'languages-declare-mps-tasks'.dependsOn downloadDependencies
tasks.'distribution-fetchDependencies'.dependsOn tasks.'languages-build'

defaultTasks 'distribution-build'
